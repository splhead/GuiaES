apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.silas.guiaes.app"
        minSdkVersion 19
        targetSdkVersion 27
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
//            shrinkResources true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

sourceSets {
    test {
        java.srcDir 'src/test'
    }

    /*androidTest {
        java.srcDir 'src/androidTest'
    }*/
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.firebase:firebase-core:' + firebase_core_version
    implementation 'com.google.firebase:firebase-ads:' + firebase_ads_version
    implementation 'com.android.support:customtabs:' + support_version
    implementation 'com.android.support:support-v4:' + support_version
    implementation 'com.android.support:appcompat-v7:' + support_version
    implementation 'com.android.support:preference-v7:' + support_version
    implementation 'com.android.support:design:' + support_version
    implementation 'com.android.support:recyclerview-v7:' + support_version
    implementation 'com.android.support:cardview-v7:' + support_version
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.code.gson:gson:' + gson_version
    implementation 'com.crashlytics.sdk.android:crashlytics:' + crashlytics_version
    implementation 'com.google.firebase:firebase-perf:' + firebase_perf_version
    implementation 'com.github.ViksaaSkool:AwesomeSplash:v1.0.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

}

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    }
    catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        def saida = stdout.toString().trim().split('-')
        return saida[0]
    }
    catch (ignored) {
        return null
    }
}
android {
    defaultConfig {
        versionCode getVersionCode()
        versionName getVersionName()
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}


apply plugin: 'com.google.gms.google-services'
